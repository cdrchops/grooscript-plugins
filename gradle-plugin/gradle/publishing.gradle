buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'gradle-plugin'
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''

    publications = ['mavenJava']

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'grooscript'
        name = "org.grooscript:gradle-plugin"
        desc = 'Gradle plugin to convert groovy code to javascript'
        websiteUrl = 'http://grooscript.org/'
        issueTrackerUrl = 'https://github.com/chiquitinxx/grooscript-plugins/issues'
        vcsUrl = 'https://github.com/chiquitinxx/grooscript-plugins.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'javascript', 'grooscript', 'gradle', 'plugin']
        version {
            name = '1.3.0'
            desc = 'Upgrade grooscript libs and add grails tasks'
            vcsTag = '1.3.0'
            attributes = [
                    'gradle-plugin': ['org.grooscript.conversion', 'org.grooscript', 'gradle-plugin'].join(':')
            ]
        }
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}